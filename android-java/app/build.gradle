plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

// Load env.properties file for Agora configuration
def envProperties = new Properties()
def envPropertiesFile = rootProject.file("env.properties")
if (envPropertiesFile.exists()) {
    envPropertiesFile.withInputStream { envProperties.load(it) }
}

// Validate required Agora configuration properties
def requiredProperties = [
    "agora.appId",
    "agora.restKey",
    "agora.restSecret",
    "agora.pipelineId"
]

def missingProperties = []
requiredProperties.each { key ->
    def value = envProperties.getProperty(key)
    if (value == null || value.trim().isEmpty()) {
        missingProperties.add(key)
    }
}

if (!missingProperties.isEmpty()) {
    def errorMessage = new StringBuilder()
    errorMessage.append("Please configure the following required properties in env.properties:\n")
    missingProperties.each { prop ->
        errorMessage.append("  - ${prop}\n")
    }
    errorMessage.append("\nPlease refer to env.properties for configuration reference.")
    throw new GradleException(errorMessage.toString())
}

android {
    namespace 'io.agora.convoai.example.startup'
    compileSdk 36

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    defaultConfig {
        applicationId "io.agora.agent.example.startup.java"
        minSdk 26
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Load Agora configuration from env.properties
        buildConfigField "String", "AGORA_APP_ID", "\"${envProperties.getProperty("agora.appId", "")}\""
        buildConfigField "String", "AGORA_APP_CERTIFICATE", "\"${envProperties.getProperty("agora.appCertificate", "")}\""
        buildConfigField "String", "REST_KEY", "\"${envProperties.getProperty("agora.restKey", "")}\""
        buildConfigField "String", "REST_SECRET", "\"${envProperties.getProperty("agora.restSecret", "")}\""
        buildConfigField "String", "PIPELINE_ID", "\"${envProperties.getProperty("agora.pipelineId", "")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    implementation libs.okhttp.core
    implementation libs.okhttp.logging.interceptor
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.gson
    implementation libs.agora.rtc
    implementation libs.agora.rtm
    
    // Lifecycle components (Java version, no -ktx suffix)
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.lifecycle.livedata
    
    // RecyclerView
    implementation libs.androidx.recyclerview
    
    // Navigation Component (Java version, no -ktx suffix)
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui
}
